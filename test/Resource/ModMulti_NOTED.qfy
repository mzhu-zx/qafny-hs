method EntangleOne1(q : qreg[1], p : qreg[10], base : nat)
  requires { q [0 .. 1] : had ↦ ⊗ i . (1) }
  requires { p [0 .. 10] : en ↦ ∑ i ∈ [0 .. 1]. (1) }
  ensures { q [0 .. 1], p [0 .. 10] : en ↦ ∑ k ∈ [0 .. 2]. (k, ((Pow(base, k)) % 10)) }
{
  if (q[0 .. 1])
    separates p[0 .. 10]
  {
     p[0 .. 10] *= λ (x => ((Pow(base, 1)) * x) % 10);
  }
}

method EntangleOne(q : qreg[10], p : qreg[10], base : nat)
  requires { q [0 .. 10] : had ↦ ⊗ i . (1) }
  requires { p [0 .. 10] : en ↦ ∑ i ∈ [0 .. 1]. (1) }
  ensures { q [0 .. 1], p [0 .. 10] : en ↦ ∑ k ∈ [0 .. 2]. (k, ((Pow(base, k)) % 10)) }
  ensures { q [1 .. 10] : had ↦ ⊗ i . (1) }
{
  if (q[0 .. 1])
    separates p[0 .. 10]
  {
     p[0 .. 10] *= λ (x => ((Pow(base, 1)) * x) % 10);
  }
}  

method MultiMod(q : qreg[10], p : qreg[10], base : nat )
  requires base >= 3
  requires { q[1 .. 10] : had ↦ ⊗ i . (1) }
  requires { q[0 .. 1], p[0 .. 10] : en ↦
    ∑ k ∈ [0 .. 2] . (k, ((Pow(base, k)) % 10))
  }
  ensures { q[0 .. 10], p[0 .. 10] : en ↦
    ∑ k ∈ [0 .. Pow2(10)] . (k, ((Pow(base, k)) % 10))
  }
{
  // Some notes on how to compile this.
  // 
  // 1. Understand that for the precondition where q[0..0], it holds vacuously
  //    true because there's no qubit at all.
  //    Implementationwise, I need a way to skip emitting assertions for empty 
  //    ranges
  // 2. With the current semantics, in each iteration, I duplicate both ranges.
  //    This is decided by the fact that q[i .. i+1] is indeed a Had partition.
  // 3. Merge turns to be trickier? 
  //    I need a way to do q[ 0 .. i ]  + q [i .. i + 1] provided that they're 
  //    of different types.
  //    I can use the fact that in the invariant typing q[i .. i + 1] is
  //    adjacent to q[0..i] in entanglement with p[0 .. 10] to make it work so that I just add 0 to one half and
  //    add Pow(i) to another
  // 4. This semantics should be documented seriously for our Dafny 2024
  //    submission.  

  for i in [1 .. 10] with q[i .. i + 1]
    separates q[0 .. i], p[0 .. 10]
    invariant {
      q[0 .. i], p[0 .. 10] : en ↦ ∑ k ∈ [0 .. Pow2(i)] . ( k , ((Pow(base, k)) % 10) )
    }
    invariant {
      q [i .. 10] : had ↦ ⊗ k . (1)
    }   
  {
    p[0 .. 10] *= λ (x => ((Pow(base, Pow2(i))) * x) % 10);

    // This is super hacky, but I have no good way to do this now....
    #LemmaPowEquiv(p_seq'nat'_7__emit, base, i, 10);
  }
}  


#lemma LemmaPowEquiv(s : seq<nat>, a : nat, i : nat, N : nat)
#  requires |s| == Pow2(i) && N >= 2
#  requires forall k | 0 <= k < Pow2(i) :: s[k] == ((Pow(a, Pow2(i)) * (Pow(a, k) % N)) % N)
#  ensures forall k | 0 <= k < Pow2(i) :: s[k] == (Pow(a, (Pow2(i) + k)) % N)
#{ 
#  forall k | 0 <= k < Pow2(i) {
#    calc == {
#      s[k] == ((Pow(a, Pow2(i)) * (Pow(a, k) % N)) % N);
#     { LemmaMulModNoopRightAuto(); } // crush double Ns
#      s[k] == ((Pow(a, Pow2(i)) * Pow(a, k)) % N);
#     { LemmaPowAdds(a, Pow2(i), k); } // crush the add on Power
#     s[k] == (Pow(a, (Pow2(i) + k)) % N);
#    }
#  }
#} 
